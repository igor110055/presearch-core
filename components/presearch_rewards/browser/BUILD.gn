import("//presearch/components/presearch_rewards/browser/buildflags/buildflags.gni")
import("//presearch/components/ipfs/buildflags/buildflags.gni")
import("//presearch/vendor/bat-native-ledger/config.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browser") {
  # Remove when https://github.com/presearch/presearch-browser/issues/10618 is resolved
  check_includes = false
  public_deps = [ "buildflags" ]

  defines = [
    "BITFLYER_CLIENT_ID=\"$bitflyer_client_id\"",
    "BITFLYER_CLIENT_SECRET=\"$bitflyer_client_secret\"",
    "BITFLYER_STAGING_CLIENT_ID=\"$bitflyer_staging_client_id\"",
    "BITFLYER_STAGING_CLIENT_SECRET=\"$bitflyer_staging_client_secret\"",
    "BITFLYER_STAGING_URL=\"$bitflyer_staging_url\"",
    "UPHOLD_CLIENT_ID=\"$uphold_client_id\"",
    "UPHOLD_CLIENT_SECRET=\"$uphold_client_secret\"",
    "UPHOLD_STAGING_CLIENT_ID=\"$uphold_staging_client_id\"",
    "UPHOLD_STAGING_CLIENT_SECRET=\"$uphold_staging_client_secret\"",
  ]

  sources = [
    "android_util.cc",
    "android_util.h",
    "diagnostic_log.cc",
    "diagnostic_log.h",
    "logging.h",
    "rewards_notification_service.cc",
    "rewards_notification_service.h",
    "rewards_notification_service_observer.h",
    "rewards_p3a.cc",
    "rewards_p3a.h",
    "rewards_protocol_handler.cc",
    "rewards_protocol_handler.h",
    "rewards_service.cc",
    "rewards_service.h",
    "rewards_service_observer.h",
    "static_values.h",
    "switches.cc",
    "switches.h",
  ]

  deps = [
    "//base",
    "//presearch/components/presearch_rewards/common",

    # for profile.h
    "//components/domain_reliability",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/sessions",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
  ]

  if (!is_android) {
    # on android, one shot is part of base lib
    deps += [
      # for one shot event
      "//extensions/common",
    ]
  }

  if (presearch_rewards_enabled) {
    sources += [
      "net/network_delegate_helper.cc",
      "net/network_delegate_helper.h",
      "rewards_notification_service_impl.cc",
      "rewards_notification_service_impl.h",
      "rewards_service_impl.cc",
      "rewards_service_impl.h",
    ]

    deps += [
      "//presearch/app:presearch_generated_resources_grit",
      "//presearch/components/presearch_ads/browser/buildflags",
      "//presearch/components/resources",
      "//presearch/components/services/bat_ledger/public/cpp",
      "//presearch/vendor/bat-native-ads",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//url",
    ]

    if (ipfs_enabled) {
      deps += [ "//presearch/components/ipfs" ]
    }

    public_deps += [ "//presearch/vendor/bat-native-ledger:headers" ]

    if (is_android) {
      deps += [ "//presearch/components/safetynet:android" ]
    }
  }
}

source_set("testutil") {
  testonly = true
  deps = [
    ":browser",
    "//base",
    "//presearch/browser",
    "//presearch/vendor/bat-native-ledger:headers",
    "//chrome/test:test_support",
    "//content/public/browser",
    "//testing/gtest",
  ]

  sources = [
    "test_util.cc",
    "test_util.h",
  ]
}
