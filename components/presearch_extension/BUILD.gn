import("//presearch/components/common/typescript.gni")
import("//build/config/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

assert(enable_extensions)

pack_web_resources("generated_resources") {
  resource_name = "presearch_extension"
  output_dir = "$root_gen_dir/presearch/components/presearch_extension"
  # need wepack and GRD generation first
  deps = [
    "extension/presearch_extension"
  ]
}

grit("static_resources") {
  source = "extension/resources.grd"

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  outputs = [
    "grit/presearch_extension.h",
    "grit/presearch_extension_resources_map.cc",
    "grit/presearch_extension_resources_map.h",
    "presearch_extension_static.pak",
  ]

  deps = [
    "extension/presearch_extension",
  ]

  resource_ids = "//presearch/browser/resources/resource_ids"
  output_dir = "$root_gen_dir/presearch/components/presearch_extension"
}

repack("resources") {
  deps = [
    ":static_resources",
    ":generated_resources",
  ]

  sources = [
    "$root_gen_dir/presearch/components/presearch_extension/presearch_extension_static.pak",
    "$root_gen_dir/presearch/components/presearch_extension/presearch_extension_generated.pak",
  ]

  output = "$root_gen_dir/presearch/components/presearch_extension/presearch_extension_resources.pak"
}
