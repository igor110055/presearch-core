import("//presearch/browser/translate/buildflags/buildflags.gni")
import("//presearch/components/presearch_referrals/buildflags/buildflags.gni")
import("//presearch/components/presearch_wallet/common/buildflags/buildflags.gni")
import("//presearch/components/presearch_webtorrent/browser/buildflags/buildflags.gni")
import("//presearch/components/decentralized_dns/buildflags/buildflags.gni")
import("//presearch/components/ipfs/buildflags/buildflags.gni")
import("//build/config/features.gni")

# Refer to the keyed API spec for more details about the Presearch Services Key
defines = presearch_service_key_defines

source_set("net") {
  # Remove when https://github.com/presearch/presearch-browser/issues/10659 is resolved
  check_includes = false
  configs += [ "//presearch/build/geolocation" ]
  sources = [
    "presearch_ad_block_csp_network_delegate_helper.cc",
    "presearch_ad_block_csp_network_delegate_helper.h",
    "presearch_ad_block_tp_network_delegate_helper.cc",
    "presearch_ad_block_tp_network_delegate_helper.h",
    "presearch_block_safebrowsing_urls.cc",
    "presearch_block_safebrowsing_urls.h",
    "presearch_common_static_redirect_network_delegate_helper.cc",
    "presearch_common_static_redirect_network_delegate_helper.h",
    "presearch_httpse_network_delegate_helper.cc",
    "presearch_httpse_network_delegate_helper.h",
    "presearch_proxying_url_loader_factory.cc",
    "presearch_proxying_url_loader_factory.h",
    "presearch_proxying_web_socket.cc",
    "presearch_proxying_web_socket.h",
    "presearch_request_handler.cc",
    "presearch_request_handler.h",
    "presearch_site_hacks_network_delegate_helper.cc",
    "presearch_site_hacks_network_delegate_helper.h",
    "presearch_static_redirect_network_delegate_helper.cc",
    "presearch_static_redirect_network_delegate_helper.h",
    "presearch_stp_util.cc",
    "presearch_stp_util.h",
    "presearch_system_request_handler.cc",
    "presearch_system_request_handler.h",
    "global_privacy_control_network_delegate_helper.cc",
    "global_privacy_control_network_delegate_helper.h",
    "resource_context_data.cc",
    "resource_context_data.h",
    "url_context.cc",
    "url_context.h",
  ]

  deps = [
    "//base",
    "//presearch/app:presearch_generated_resources_grit",
    "//presearch/browser/safebrowsing",
    "//presearch/browser/translate/buildflags",
    "//presearch/common",
    "//presearch/components/presearch_component_updater/browser",
    "//presearch/components/presearch_referrals/buildflags",
    "//presearch/components/presearch_shields/browser",
    "//presearch/components/presearch_shields/common",
    "//presearch/components/presearch_wallet/common/buildflags",
    "//presearch/components/presearch_webtorrent/browser/buildflags",
    "//presearch/components/decentralized_dns/buildflags",
    "//presearch/components/ipfs/buildflags",
    "//presearch/extensions:common",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/common:common_constants",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/re2",
    "//url",
  ]

  if (ipfs_enabled) {
    sources += [
      "ipfs_redirect_network_delegate_helper.cc",
      "ipfs_redirect_network_delegate_helper.h",
    ]
    deps += [
      "//presearch/components/ipfs",
      "//chrome/common:channel_info",
    ]
  }

  if (enable_presearch_referrals) {
    sources += [
      "presearch_referrals_network_delegate_helper.cc",
      "presearch_referrals_network_delegate_helper.h",
    ]

    deps += [ "//presearch/components/presearch_referrals/browser" ]
  }

  if (enable_presearch_webtorrent) {
    sources += [
      "presearch_torrent_redirect_network_delegate_helper.cc",
      "presearch_torrent_redirect_network_delegate_helper.h",
    ]

    deps += [
      "//presearch/components/presearch_webtorrent/browser/",
      "//extensions/browser",
    ]
  }

  if (enable_presearch_translate_go) {
    sources += [
      "presearch_translate_redirect_network_delegate_helper.cc",
      "presearch_translate_redirect_network_delegate_helper.h",
    ]
  }

  if (decentralized_dns_enabled && presearch_wallet_enabled) {
    sources += [
      "decentralized_dns_network_delegate_helper.cc",
      "decentralized_dns_network_delegate_helper.h",
    ]

    deps += [
      "//presearch/components/presearch_wallet/browser",
      "//presearch/components/decentralized_dns",
    ]
  }
}
