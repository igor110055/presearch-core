# Copyright (c) 2021 The Presearch Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//presearch/components/presearch_rewards/browser/buildflags/buildflags.gni")
import("//presearch/components/greaselion/browser/buildflags/buildflags.gni")
import("//presearch/components/ipfs/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

presearch_browser_presearch_rewards_sources = []
presearch_browser_presearch_rewards_deps = []

if (presearch_rewards_enabled) {
  presearch_browser_presearch_rewards_sources += [
    "//presearch/browser/presearch_rewards/rewards_service_factory.cc",
    "//presearch/browser/presearch_rewards/rewards_service_factory.h",
    "//presearch/browser/presearch_rewards/rewards_tab_helper.cc",
    "//presearch/browser/presearch_rewards/rewards_tab_helper.h",
  ]

  presearch_browser_presearch_rewards_deps += [
    "//base",
    "//presearch/browser/profiles",
    "//presearch/browser/profiles:util",
    "//presearch/components/presearch_rewards/browser",
    "//presearch/components/presearch_rewards/common",
    "//presearch/components/ipfs/buildflags",
    "//presearch/vendor/bat-native-ledger:headers",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//components/keyed_service/content",
    "//components/prefs",
    "//components/sessions",
    "//extensions/buildflags",
  ]

  if (enable_greaselion) {
    presearch_browser_presearch_rewards_deps +=
        [ "//presearch/components/greaselion/browser" ]
  }

  if (enable_extensions) {
    presearch_browser_presearch_rewards_sources += [
      "//presearch/browser/presearch_rewards/extension_rewards_notification_service_observer.cc",
      "//presearch/browser/presearch_rewards/extension_rewards_notification_service_observer.h",
      "//presearch/browser/presearch_rewards/extension_rewards_service_observer.cc",
      "//presearch/browser/presearch_rewards/extension_rewards_service_observer.h",
    ]

    presearch_browser_presearch_rewards_deps += [
      "//presearch/common/extensions/api",
      "//extensions/browser",
    ]
  }

  if (ipfs_enabled) {
    presearch_browser_presearch_rewards_deps += [ "//presearch/components/ipfs" ]
  }

  if (is_android) {
    presearch_browser_presearch_rewards_sources += [
      "//presearch/browser/presearch_rewards/android/presearch_rewards_native_worker.cc",
      "//presearch/browser/presearch_rewards/android/presearch_rewards_native_worker.h",
    ]

    presearch_browser_presearch_rewards_deps += [
      "//presearch/browser/presearch_ads",
      "//presearch/components/presearch_ads/browser",
    ]
  } else {
    presearch_browser_presearch_rewards_sources += [
      "//presearch/browser/presearch_rewards/tip_dialog.cc",
      "//presearch/browser/presearch_rewards/tip_dialog.h",
    ]

    presearch_browser_presearch_rewards_deps += [
      "//presearch/common",
      "//components/guest_view/browser",
      "//components/web_modal",
      "//ui/web_dialogs",
    ]
  }
}

if (!is_android) {
  presearch_browser_presearch_rewards_deps +=
      [ "//presearch/components/presearch_rewards/common:features" ]
}
