import("//presearch/browser/translate/buildflags/buildflags.gni")
import("//presearch/build/config.gni")
import("//presearch/components/presearch_rewards/browser/buildflags/buildflags.gni")
import("//presearch/components/presearch_wallet/common/buildflags/buildflags.gni")
import("//presearch/components/presearch_wayback_machine/buildflags/buildflags.gni")
import("//presearch/components/decentralized_dns/buildflags/buildflags.gni")
import("//presearch/components/ipfs/buildflags/buildflags.gni")
import("//presearch/components/sidebar/buildflags/buildflags.gni")
import("//presearch/components/speedreader/buildflags.gni")
import("//presearch/components/tor/buildflags/buildflags.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")
import("//components/gcm_driver/config.gni")
import("//third_party/widevine/cdm/widevine.gni")

source_set("favicon_source") {
  # Remove when https://github.com/presearch/presearch-browser/issues/10614 is resolved
  check_includes = false
  sources = [
    "//chrome/browser/ui/webui/favicon_source.cc",
    "//chrome/browser/ui/webui/favicon_source.h",
  ]

  deps = [
    "//components/favicon/core",
    "//components/favicon_base",
    "//components/history/core/browser",
    "//net",
    "//ui/native_theme",
    "//url",
  ]
}

source_set("ui") {
  # Remove when https://github.com/presearch/presearch-browser/issues/10614 is resolved
  check_includes = false
  sources = [
    "webui/presearch_adblock_ui.cc",
    "webui/presearch_adblock_ui.h",
    "webui/presearch_web_ui_controller_factory.cc",
    "webui/presearch_web_ui_controller_factory.h",
    "webui/presearch_webui_source.cc",
    "webui/presearch_webui_source.h",
    "webui/webcompat_reporter_ui.cc",
    "webui/webcompat_reporter_ui.h",
  ]

  if (ipfs_enabled) {
    sources += [
      "webui/ipfs_ui.cc",
      "webui/ipfs_ui.h",
    ]
  }

  if (!is_android) {
    sources += [
      "presearch_browser.cc",
      "presearch_browser.h",
      "presearch_browser_command_controller.cc",
      "presearch_browser_command_controller.h",
      "presearch_browser_content_setting_bubble_model_delegate.cc",
      "presearch_browser_content_setting_bubble_model_delegate.h",
      "presearch_browser_window.h",
      "presearch_layout_constants.cc",
      "presearch_layout_constants.h",
      "presearch_pages.cc",
      "presearch_pages.h",
      "presearch_view_ids.h",
      "browser_commands.cc",
      "browser_commands.h",
      "browser_dialogs.h",
      "content_settings/presearch_autoplay_blocked_image_model.cc",
      "content_settings/presearch_autoplay_blocked_image_model.h",
      "content_settings/presearch_autoplay_content_setting_bubble_model.cc",
      "content_settings/presearch_autoplay_content_setting_bubble_model.h",
      "content_settings/presearch_content_setting_image_models.cc",
      "content_settings/presearch_content_setting_image_models.h",
      "omnibox/presearch_omnibox_client_impl.cc",
      "omnibox/presearch_omnibox_client_impl.h",
      "startup/default_presearch_browser_prompt.cc",
      "startup/default_presearch_browser_prompt.h",
      "toolbar/presearch_app_menu_model.cc",
      "toolbar/presearch_app_menu_model.h",
      "toolbar/presearch_recent_tabs_sub_menu_model.h",
      "webui/presearch_settings_ui.cc",
      "webui/presearch_settings_ui.h",
      "webui/presearch_welcome_ui.cc",
      "webui/presearch_welcome_ui.h",
      "webui/navigation_bar_data_provider.cc",
      "webui/navigation_bar_data_provider.h",
      "webui/new_tab_page/presearch_new_tab_message_handler.cc",
      "webui/new_tab_page/presearch_new_tab_message_handler.h",
      "webui/new_tab_page/presearch_new_tab_ui.cc",
      "webui/new_tab_page/presearch_new_tab_ui.h",
      "webui/new_tab_page/presearch_new_tab_ui_utils.cc",
      "webui/new_tab_page/presearch_new_tab_ui_utils.h",
      "webui/new_tab_page/instant_service_message_handler.cc",
      "webui/new_tab_page/instant_service_message_handler.h",
      "webui/settings/presearch_appearance_handler.cc",
      "webui/settings/presearch_appearance_handler.h",
      "webui/settings/presearch_import_data_handler.h",
      "webui/settings/presearch_privacy_handler.cc",
      "webui/settings/presearch_privacy_handler.h",
      "webui/settings/presearch_sync_handler.cc",
      "webui/settings/presearch_sync_handler.h",
      "webui/settings/default_presearch_shields_handler.cc",
      "webui/settings/default_presearch_shields_handler.h",
    ]

    if (is_mac) {
      sources += [ "webui/settings/presearch_import_data_handler_mac.mm" ]
    } else {
      sources += [ "webui/settings/presearch_import_data_handler.cc" ]
    }

    if (enable_sparkle) {
      sources += [
        "webui/settings/presearch_relaunch_handler_mac.h",
        "webui/settings/presearch_relaunch_handler_mac.mm",
      ]
    }
  }

  if (toolkit_views) {
    sources += [
      "views/bookmarks/bookmark_bar_instructions_view.cc",
      "views/bookmarks/bookmark_bar_instructions_view.h",
      "views/presearch_default_browser_dialog_view.cc",
      "views/presearch_default_browser_dialog_view.h",
      "views/presearch_layout_provider.cc",
      "views/presearch_layout_provider.h",
      "views/download/presearch_download_item_view.cc",
      "views/download/presearch_download_item_view.h",
      "views/frame/presearch_browser_view.cc",
      "views/frame/presearch_browser_view.h",
      "views/frame/presearch_opaque_browser_frame_view.cc",
      "views/frame/presearch_opaque_browser_frame_view.h",
      "views/frame/presearch_window_frame_graphic.cc",
      "views/frame/presearch_window_frame_graphic.h",
      "views/reader_mode/presearch_reader_mode_icon_view.cc",
      "views/reader_mode/presearch_reader_mode_icon_view.h",
      "views/rounded_separator.cc",
      "views/rounded_separator.h",
      "views/tabs/presearch_browser_tab_strip_controller.cc",
      "views/tabs/presearch_browser_tab_strip_controller.h",
      "views/tabs/presearch_tab_context_menu_contents.cc",
      "views/tabs/presearch_tab_context_menu_contents.h",
      "views/tabs/presearch_tab_strip.cc",
      "views/tabs/presearch_tab_strip.h",
      "views/toolbar/bookmark_button.cc",
      "views/toolbar/bookmark_button.h",
    ]

    if (enable_sparkle) {
      sources += [
        "views/update_recommended_message_box_mac.h",
        "views/update_recommended_message_box_mac.mm",
      ]
    }

    if (enable_presearch_translate_extension) {
      sources += [
        "views/translate/presearch_translate_bubble_view.cc",
        "views/translate/presearch_translate_bubble_view.h",
        "views/translate/presearch_translate_icon_view.cc",
        "views/translate/presearch_translate_icon_view.h",
      ]
    }

    if (is_win) {
      sources += [
        "views/frame/presearch_glass_browser_frame_view.cc",
        "views/frame/presearch_glass_browser_frame_view.h",
      ]
    }

    if (is_mac) {
      sources += [
        "views/frame/presearch_browser_non_client_frame_view_mac.h",
        "views/frame/presearch_browser_non_client_frame_view_mac.mm",
      ]
    }
  }

  if (is_win || is_mac || is_linux) {
    sources += [
      "views/profiles/presearch_incognito_menu_view.cc",
      "views/profiles/presearch_incognito_menu_view.h",
      "views/profiles/presearch_profile_menu_view.cc",
      "views/profiles/presearch_profile_menu_view.h",
    ]
  }

  if (is_win || is_mac) {
    sources += [
      "views/frame/presearch_browser_frame.cc",
      "views/frame/presearch_browser_frame.h",
    ]
  }

  if (is_linux) {
    sources += [
      "views/presearch_views_delegate_linux.cc",
      "views/presearch_views_delegate_linux.h",
    ]
  }

  deps = [
    "tabs",
    "//base",
    "//presearch/app:command_ids",
    "//presearch/app/theme:presearch_theme_resources",
    "//presearch/app/theme:presearch_unscaled_resources",
    "//presearch/app/vector_icons:vector_icons",
    "//presearch/browser:sparkle_buildflags",
    "//presearch/browser/presearch_ads",
    "//presearch/browser/devtools",
    "//presearch/browser/gcm_driver",
    "//presearch/browser/profiles",
    "//presearch/browser/tor",

    # //chrome/browser/ui depends on //presearch/browser/ui, add this target here
    # to pull in dependencies needed for the overwrite codes in chromium_src.
    "//presearch/chromium_src/chrome/browser/ui",
    "//presearch/common",
    "//presearch/common:pref_names",
    "//presearch/components/presearch_adblock_ui:generated_resources",
    "//presearch/components/presearch_ads/browser",
    "//presearch/components/presearch_rewards/browser",
    "//presearch/components/presearch_rewards/resources",
    "//presearch/components/presearch_shields/browser",
    "//presearch/components/presearch_wallet/common/buildflags",
    "//presearch/components/presearch_wayback_machine:buildflags",
    "//presearch/components/cosmetic_filters/resources/data:generated_resources",
    "//presearch/components/crypto_dot_com/browser/buildflags:buildflags",
    "//presearch/components/decentralized_dns/buildflags",
    "//presearch/components/l10n/browser",
    "//presearch/components/l10n/common",
    "//presearch/components/ntp_background_images/browser",
    "//presearch/components/ntp_background_images/common",
    "//presearch/components/ntp_widget_utils/browser",
    "//presearch/components/p3a:buildflags",
    "//presearch/components/sidebar/buildflags",
    "//presearch/components/tor:pref_names",
    "//presearch/components/vector_icons",
    "//presearch/components/webcompat_reporter/browser",
    "//presearch/components/webcompat_reporter/ui:generated_resources",
    "//presearch/components/weekly_storage",
    "//presearch/ui/presearch_ads",
    "//presearch/ui/presearch_ads/public/cpp",
    "//chrome/app:command_ids",
    "//chrome/app/vector_icons:vector_icons",
    "//chrome/browser:browser_process",
    "//chrome/common",
    "//chrome/services/qrcode_generator",
    "//components/content_settings/browser",
    "//components/gcm_driver:gcm_buildflags",
    "//components/prefs",
    "//components/sessions",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/cpp",
    "//skia",
    "//ui/accessibility",
    "//ui/base",
    "//ui/gfx",
    "//ui/resources",
    "//url",
  ]

  # This is no longer compiled into Chromium on Android, but we still
  # need it
  if (is_android) {
    deps += [ ":favicon_source" ]
  }

  if (enable_presearch_wayback_machine) {
    deps += [ "//presearch/components/presearch_wayback_machine" ]

    if (toolkit_views) {
      deps += [ "//presearch/browser/ui/views/infobars:presearch_wayback_machine" ]
    }
  }

  if (ipfs_enabled) {
    deps += [
      "//presearch/components/ipfs",
      "//presearch/components/ipfs_ui:generated_resources",
    ]
  }

  if (enable_sidebar) {
    deps += [
      "//presearch/browser/ui/sidebar",
      "//presearch/components/sidebar",
    ]

    sources += [
      "views/frame/presearch_contents_layout_manager.cc",
      "views/frame/presearch_contents_layout_manager.h",
      "views/sidebar/bubble_border_with_arrow.cc",
      "views/sidebar/bubble_border_with_arrow.h",
      "views/sidebar/sidebar_add_item_bubble_delegate_view.cc",
      "views/sidebar/sidebar_add_item_bubble_delegate_view.h",
      "views/sidebar/sidebar_bubble_background.cc",
      "views/sidebar/sidebar_bubble_background.h",
      "views/sidebar/sidebar_button_view.cc",
      "views/sidebar/sidebar_button_view.h",
      "views/sidebar/sidebar_container_view.cc",
      "views/sidebar/sidebar_container_view.h",
      "views/sidebar/sidebar_control_view.cc",
      "views/sidebar/sidebar_control_view.h",
      "views/sidebar/sidebar_item_added_feedback_bubble.cc",
      "views/sidebar/sidebar_item_added_feedback_bubble.h",
      "views/sidebar/sidebar_item_drag_context.cc",
      "views/sidebar/sidebar_item_drag_context.h",
      "views/sidebar/sidebar_item_view.cc",
      "views/sidebar/sidebar_item_view.h",
      "views/sidebar/sidebar_items_contents_view.cc",
      "views/sidebar/sidebar_items_contents_view.h",
      "views/sidebar/sidebar_items_scroll_view.cc",
      "views/sidebar/sidebar_items_scroll_view.h",
    ]
  }

  if (!is_android) {
    deps += [
      "//presearch/app:presearch_generated_resources_grit",
      "//presearch/browser:version_info",
      "//presearch/browser/resources/settings:resources",
      "//presearch/browser/ui/bookmark",
      "//presearch/components/presearch_new_tab_ui:generated_resources",
      "//presearch/components/presearch_sync",
      "//presearch/components/presearch_sync:profile_sync_service_helper",
      "//presearch/components/presearch_welcome_ui:generated_resources",
      "//chrome/app:chromium_strings",
      "//components/sync_device_info",
      "//third_party/blink/public/common",
    ]
  }

  if (enable_extensions && toolkit_views) {
    sources += [
      "presearch_actions/presearch_action_icon_with_badge_image_source.cc",
      "presearch_actions/presearch_action_icon_with_badge_image_source.h",
      "presearch_actions/presearch_action_view_controller.cc",
      "presearch_actions/presearch_action_view_controller.h",
      "toolbar/presearch_location_bar_model_delegate.cc",
      "toolbar/presearch_location_bar_model_delegate.h",
      "toolbar/presearch_toolbar_actions_model.cc",
      "toolbar/presearch_toolbar_actions_model.h",
      "views/presearch_actions/presearch_action_view.cc",
      "views/presearch_actions/presearch_action_view.h",
      "views/presearch_actions/presearch_actions_container.cc",
      "views/presearch_actions/presearch_actions_container.h",
      "views/location_bar/presearch_location_bar_view.cc",
      "views/location_bar/presearch_location_bar_view.h",
      "views/location_bar/presearch_star_view.cc",
      "views/location_bar/presearch_star_view.h",
      "views/profiles/presearch_avatar_toolbar_button.cc",
      "views/profiles/presearch_avatar_toolbar_button.h",
      "views/tabs/presearch_alert_indicator.cc",
      "views/tabs/presearch_alert_indicator.h",
      "views/tabs/presearch_new_tab_button.cc",
      "views/tabs/presearch_new_tab_button.h",
      "views/toolbar/presearch_toolbar_view.cc",
      "views/toolbar/presearch_toolbar_view.h",
    ]

    if (enable_extensions) {
      sources += [
        "webui/settings/presearch_default_extensions_handler.cc",
        "webui/settings/presearch_default_extensions_handler.h",
      ]

      deps += [
        "//presearch/components/ipfs/buildflags",
        "//third_party/widevine/cdm:buildflags",
      ]

      if (enable_widevine) {
        deps += [ "//presearch/browser/widevine" ]
      }
    }

    if (decentralized_dns_enabled) {
      deps += [ "//presearch/components/decentralized_dns" ]
    }

    if (presearch_rewards_enabled) {
      sources += [
        "views/presearch_actions/presearch_rewards_action_stub_view.cc",
        "views/presearch_actions/presearch_rewards_action_stub_view.h",
      ]
    }

    if (enable_tor) {
      sources += [
        "views/location_bar/onion_location_view.cc",
        "views/location_bar/onion_location_view.h",
      ]
    }

    if (ipfs_enabled) {
      sources += [
        "views/location_bar/ipfs_location_view.cc",
        "views/location_bar/ipfs_location_view.h",
      ]
    }

    deps += [
      "//presearch/browser/extensions",
      "//presearch/browser/resources/extensions:resources",
      "//presearch/components/presearch_extension:generated_resources",
      "//presearch/components/presearch_extension:static_resources",
      "//presearch/components/resources:static_resources",
      "//chrome/browser/extensions",
      "//components/sessions",
      "//extensions/browser",
      "//extensions/common:common_constants",
    ]
  }

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/ui/webui/help/version_updater_win.cc",
      "//chrome/browser/ui/webui/help/version_updater_win.h",
    ]
    deps += [ "//google_update" ]
  }

  if (presearch_rewards_enabled) {
    sources += [
      "webui/presearch_rewards_internals_ui.cc",
      "webui/presearch_rewards_internals_ui.h",
      "webui/presearch_rewards_page_ui.cc",
      "webui/presearch_rewards_page_ui.h",
      "webui/presearch_rewards_source.cc",
      "webui/presearch_rewards_source.h",
    ]

    if (!is_android) {
      sources += [
        "webui/presearch_tip_ui.cc",
        "webui/presearch_tip_ui.h",
      ]
    }

    deps += [
      "//presearch/browser/presearch_ads",
      "//presearch/components/presearch_ads/browser",
      "//presearch/components/presearch_rewards/resources:internals_generated_resources",
      "//presearch/components/presearch_rewards/resources:page_generated_resources",
      "//presearch/components/presearch_rewards/resources:tip_generated_resources",
      "//presearch/vendor/bat-native-ledger:headers",
    ]
  }

  if (enable_tor) {
    sources += [
      "webui/tor_internals_ui.cc",
      "webui/tor_internals_ui.h",
    ]

    deps += [ "//presearch/components/tor/resources:internals_generated_resources" ]
  }

  if (presearch_wallet_enabled && !is_android) {
    sources += [
      "webui/presearch_wallet_ui.cc",
      "webui/presearch_wallet_ui.h",
    ]
    deps += [ "//presearch/components/presearch_wallet_ui:generated_resources" ]
  }

  if (toolkit_views) {
    deps += [
      "//ui/events",
      "//ui/views",
    ]

    if (enable_presearch_translate_extension) {
      deps += [ "//components/translate/core/browser" ]
    }

    if (enable_speedreader) {
      sources += [
        "views/toolbar/speedreader_button.cc",
        "views/toolbar/speedreader_button.h",
      ]
      deps += [
        "//presearch/components/speedreader",
        "//presearch/components/speedreader:buildflags",
      ]
    }
  }
}
