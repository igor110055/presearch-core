import("//presearch/components/decentralized_dns/buildflags/buildflags.gni")
import("//presearch/components/tor/buildflags/buildflags.gni")
import("//components/gcm_driver/config.gni")

source_set("profiles") {
  # Remove when https://github.com/brave/presearch-browser/issues/10648 is resolved
  check_includes = false
  sources = [
    "presearch_bookmark_model_loaded_observer.cc",
    "presearch_bookmark_model_loaded_observer.h",
    "presearch_profile_avatar_downloader.cc",
    "presearch_profile_avatar_downloader.h",
    "presearch_profile_impl.cc",
    "presearch_profile_impl.h",
    "presearch_profile_manager.cc",
    "presearch_profile_manager.h",
    "presearch_renderer_updater.cc",
    "presearch_renderer_updater.h",
    "presearch_renderer_updater_factory.cc",
    "presearch_renderer_updater_factory.h",
  ]

  if (is_win) {
    sources += [
      "presearch_profile_shortcut_manager_win.cc",
      "presearch_profile_shortcut_manager_win.h",
    ]
  }

  public_deps = [ ":util" ]

  deps = [
    "//base",
    "//presearch/browser/presearch_ads",
    "//presearch/browser/gcm_driver",
    "//presearch/common",
    "//presearch/common:pref_names",
    "//presearch/components/presearch_ads/browser",
    "//presearch/components/presearch_rewards/browser",
    "//presearch/components/presearch_sync:prefs",
    "//presearch/components/presearch_wallet/common/buildflags",
    "//presearch/components/content_settings/core/browser",
    "//presearch/components/decentralized_dns/buildflags",
    "//presearch/components/ipfs/buildflags",
    "//presearch/components/tor",
    "//presearch/content:browser",
    "//chrome/common",
    "//components/gcm_driver:gcm_buildflags",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/search_engines",
    "//components/signin/public/identity_manager",
    "//mojo/public/cpp/bindings",
    "//ui/base",
  ]

  if (use_gcm_from_platform) {
    deps += [ "//presearch/browser/gcm_driver" ]
  }

  if (enable_tor) {
    deps += [ "//presearch/browser/tor" ]
  }
}

source_set("util") {
  # Remove when https://github.com/brave/presearch-browser/issues/10648 is resolved
  check_includes = false

  sources = [
    "profile_util.cc",
    "profile_util.h",
  ]

  deps = [
    "//base",
    "//presearch/common",
    "//presearch/components/ntp_background_images/common",
    "//presearch/components/search_engines",
    "//presearch/components/tor/buildflags",
    "//components/prefs",
    "//content/public/browser",
  ]
}
