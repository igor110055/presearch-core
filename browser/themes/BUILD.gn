source_set("themes") {
  sources = [ "presearch_dark_mode_utils.h" ]

  deps = [
    "//base",
    "//presearch/common:pref_names",
    "//presearch/common:switches",
  ]

  if (is_android) {
    sources += [ "presearch_dark_mode_utils_android.cc" ]
  } else {
    sources += [
      "presearch_dark_mode_utils_internal.cc",
      "presearch_dark_mode_utils_internal.h",
      "theme_properties.cc",
      "theme_properties.h",
    ]

    if (is_linux) {
      sources += [ "presearch_dark_mode_utils_linux.cc" ]
    }

    if (is_mac) {
      sources += [ "presearch_dark_mode_utils_mac.mm" ]
    }

    if (is_win) {
      sources += [ "presearch_dark_mode_utils_win.cc" ]
    }

    deps += [
      "//presearch/components/sidebar/buildflags",
      "//chrome/browser:theme_properties",
      "//skia:skia",
      "//ui/gfx:color_utils",
      "//ui/native_theme:native_theme",
      "//ui/native_theme:native_theme_browser",
    ]
  }
}
